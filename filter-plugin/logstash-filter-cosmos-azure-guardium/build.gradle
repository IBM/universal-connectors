import java.nio.file.Files
import static java.nio.file.StandardCopyOption.REPLACE_EXISTING

apply plugin: 'java'

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
        classpath group: 'org.yaml', name: 'snakeyaml', version: '2.2'
    }
}

def universalConnectorsDir=project.projectDir.parentFile?.parentFile.toString();
gradle.ext.versions = new org.yaml.snakeyaml.Yaml().load( new File("${universalConnectorsDir}/versions.yml").newInputStream() )

apply plugin: 'jacoco'
apply from: LOGSTASH_CORE_PATH + "/../rubyUtils.gradle"

// ===========================================================================
// plugin info
// ===========================================================================
group                      'com.ibm.guardium.azure.cosmos' // must match the package of the main plugin class
version                    "${file("VERSION").text.trim()}" // read from required VERSION file
description                = "Azure Cosmos-Guardium filter"
pluginInfo.licenses        = ['Apache-2.0'] // list of SPDX license IDs
pluginInfo.longDescription = "This gem is a Logstash azure AzureCosmos filter plugin required to be installed as part of IBM Security Guardium, Guardium Universal connector configuration. This gem is not a stand-alone program."
pluginInfo.authors         = ['IBM']
pluginInfo.email           = ['']
pluginInfo.homepage        = "https://github.com/IBM/universal-connectors"
pluginInfo.pluginType      = "filter"
pluginInfo.pluginClass     = "AzureCosmosGuardiumFilter"
pluginInfo.pluginName      = "azure_cosmos_guardium_filter" // must match the @LogstashPlugin annotation in the main plugin class
// ===========================================================================

sourceCompatibility = 1.8
targetCompatibility = 1.8


repositories {
    mavenCentral()
}

task copyDependencyLibs(type: Copy) {
    into "dependenciesLib"
    from configurations.compileClasspath
    from configurations.runtimeClasspath
    from configurations.testCompileClasspath
    from configurations.testRuntimeClasspath
}


apply plugin: 'com.github.johnrengelman.shadow'

shadowJar {
    archiveClassifier = null
    transform(com.github.jengelman.gradle.plugins.shadow.transformers.Log4j2PluginsCacheFileTransformer)
}

dependencies {
    implementation group: 'commons-validator', name: 'commons-validator', version: versions.dependencies.commonsValidator
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: versions.dependencies.log4jCore
    implementation 'org.apache.commons:commons-lang3:'  + versions.dependencies.commonsLang
    implementation 'com.google.code.gson:gson:' + versions.dependencies.gson
    implementation group: 'commons-beanutils', name: 'commons-beanutils', version: versions.dependencies.commonsBeanutils
    implementation fileTree(dir: LOGSTASH_CORE_PATH, include: "build/libs/logstash-core-*.*.*.jar")
    implementation fileTree(dir: GUARDIUM_UNIVERSALCONNECTOR_COMMONS_PATH, include: "common-*.*.*.jar")
   
     implementation group: 'org.parboiled', name: 'parboiled-java', version: versions.dependencies.parboiledJava
     implementation group: 'org.json', name: 'json', version: versions.dependencies.json
     implementation group: 'org.glassfish', name: 'javax.json', version: versions.dependencies.javaxJson
    testImplementation'junit:junit:' + versions.dependencies.junit
    testImplementation 'org.jruby:jruby-complete:' + versions.dependencies.jrubyComplete

    testImplementation fileTree(dir: GUARDIUM_UNIVERSALCONNECTOR_COMMONS_PATH, include: "common-*.*.*.jar")
}

clean {
    delete "${projectDir}/Gemfile"
    delete "${projectDir}/" + pluginInfo.pluginFullName() + ".gemspec"
    delete "${projectDir}/lib/"
    delete "${projectDir}/dependenciesLib/"
    delete "${projectDir}/vendor/"
    new FileNameFinder().getFileNames(projectDir.toString(), pluginInfo.pluginFullName() + "-?.?.?.gem").each { filename ->
        delete filename
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.register("vendor"){
    dependsOn shadowJar
    doLast {
        String vendorPathPrefix = "vendor/jar-dependencies"
        String projectGroupPath = project.group.replaceAll('\\.', '/')
        File projectJarFile = file("${vendorPathPrefix}/${projectGroupPath}/${pluginInfo.pluginFullName()}/${project.version}/${pluginInfo.pluginFullName()}-${project.version}.jar")
        projectJarFile.mkdirs()
        Files.copy(file("$buildDir/libs/${project.name}-${project.version}.jar").toPath(), projectJarFile.toPath(), REPLACE_EXISTING)
        validatePluginJar(projectJarFile, project.group)
    }
}

tasks.register("generateRubySupportFiles") {
    doLast {
        generateRubySupportFilesForPlugin(project.description, project.group, version)
    }
}

tasks.register("removeObsoleteJars") {
    doLast {
        new FileNameFinder().getFileNames(
                projectDir.toString(),
                "vendor/**/" + pluginInfo.pluginFullName() + "*.jar",
                "vendor/**/" + pluginInfo.pluginFullName() + "-" + version + ".jar").each { f ->
            delete f
        }
    }
}

tasks.register("gem"){
    dependsOn ([downloadAndInstallJRuby, removeObsoleteJars, vendor, generateRubySupportFiles])
    doLast {
        buildGem(projectDir, buildDir, pluginInfo.pluginFullName() + ".gemspec")
    }
}
jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
    
     afterEvaluate { // (optional) : to exclude classes / packages from coverage
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(
                dir: it,
                exclude: [''])
        })
    }
}
test.finalizedBy jacocoTestReport