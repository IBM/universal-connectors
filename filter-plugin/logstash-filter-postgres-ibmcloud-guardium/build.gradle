import java.nio.file.Files
import static java.nio.file.StandardCopyOption.REPLACE_EXISTING

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'eclipse'
apply from: LOGSTASH_CORE_PATH + "/../rubyUtils.gradle"
//apply plugin: "com.github.spotbugs"

// ===========================================================================
//                             plugin info
// ===========================================================================
group                      "com.ibm.guardium.icd.postgresql" // must match the package of the main plugin class
version                    "${file("VERSION").text.trim()}" // read from required VERSION file
description                = "ICD-Postgresql-Guardium filter plugin"
pluginInfo.licenses        = ['Apache-2.0'] // list of SPDX license IDs
pluginInfo.longDescription = "This gem is a Logstash ICD-Postgresql filter plugin required to be installed as part of IBM Security Guardium, Guardium Universal connector configuration. This gem is not a stand-alone program."
pluginInfo.authors         = ['IBM', '']
pluginInfo.email           = ['']
pluginInfo.homepage        = "http://www.elastic.co/guide/en/logstash/current/index.html"
pluginInfo.pluginType      = "filter"
pluginInfo.pluginClass     = "ICDPostgresqlGuardiumFilter"
pluginInfo.pluginName      = "icd_postgresql_guardium_filter" // must match the @LogstashPlugin annotation in the main plugin class
// ===========================================================================
sourceCompatibility = 1.8
targetCompatibility = 1.8
buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:6.1.0'
        classpath group: 'org.yaml', name: 'snakeyaml', version: '2.2'
    }
}
def universalConnectorsDir=project.projectDir.parentFile?.parentFile.toString();
def versions = new org.yaml.snakeyaml.Yaml().load( new File("${universalConnectorsDir}/versions.yml").newInputStream() )

repositories {
    mavenCentral()
}

apply plugin: "com.github.johnrengelman.shadow"
//apply plugin: 'org.owasp.dependencycheck'

shadowJar {
    classifier = null
    transform(com.github.jengelman.gradle.plugins.shadow.transformers.Log4j2PluginsCacheFileTransformer)
}

dependencies {

    implementation group: 'commons-validator', name: 'commons-validator', version: versions.dependencies.commonsValidator
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: versions.dependencies.log4jCore
    implementation 'org.apache.commons:commons-lang3:' + versions.dependencies.commonsLang
    implementation 'com.google.code.gson:gson:' + versions.dependencies.gson
    implementation group: 'commons-beanutils', name: 'commons-beanutils', version: versions.dependencies.commonsBeanutils
    testImplementation group: 'org.mockito', name: 'mockito-all', version: versions.dependencies.mockitoAll
    testImplementation 'org.junit.jupiter:junit-jupiter:' + versions.dependencies.junitJupiter
    implementation fileTree(dir: LOGSTASH_CORE_PATH, include: "build/libs/logstash-core-*.*.*.jar")
    implementation fileTree(dir: GUARDIUM_UNIVERSALCONNECTOR_COMMONS_PATH, include: "common-*.*.*.jar")
    testImplementation 'junit:junit:' + versions.dependencies.junit
    testImplementation 'org.jruby:jruby-complete:' + versions.dependencies.jrubyComplete
    testImplementation fileTree(dir: GUARDIUM_UNIVERSALCONNECTOR_COMMONS_PATH, include: "common-*.*.*.jar")
}

test {
    useJUnitPlatform()
}


tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

/*spotbugs {
    ignoreFailures = true
    showStackTraces = false
    reportsDir = file("$buildDir/spotbugs")
}

spotbugsMain {
    reports {
        html {
            required = true
            outputLocation = file("$buildDir/reports/spotbugs/main/spotbugsTest.html")
            stylesheet = 'fancy-hist.xsl'
        }

    }
}
 tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }*/

clean {
    delete "${projectDir}/Gemfile"
    delete "${projectDir}/" + pluginInfo.pluginFullName() + ".gemspec"
    delete "${projectDir}/lib/"
    delete "${projectDir}/vendor/"
    new FileNameFinder().getFileNames(projectDir.toString(), pluginInfo.pluginFullName() + "-?.?.?.gem").each { filename ->
        delete filename
    }
}

task vendor(dependsOn: shadowJar) {
    doLast {
        String vendorPathPrefix = "vendor/jar-dependencies"
        String projectGroupPath = project.group.replaceAll('\\.', '/')
        File projectJarFile = file("${vendorPathPrefix}/${projectGroupPath}/${pluginInfo.pluginFullName()}/${project.version}/${pluginInfo.pluginFullName()}-${project.version}.jar")
        projectJarFile.mkdirs()
        Files.copy(file("$buildDir/libs/${project.name}-${project.version}.jar").toPath(), projectJarFile.toPath(), REPLACE_EXISTING)
        validatePluginJar(projectJarFile, project.group)
    }
}


task generateRubySupportFiles() {
    doLast {
        generateRubySupportFilesForPlugin(project.description, project.group, version)
    }
}

task removeObsoleteJars() {
    doLast {
        new FileNameFinder().getFileNames(
                projectDir.toString(),
                "vendor/**/" + pluginInfo.pluginFullName() + "*.jar",
                "vendor/**/" + pluginInfo.pluginFullName() + "-" + version + ".jar").each { f ->
            delete f
        }
    }
}
tasks.register("gem"){
    dependsOn ([downloadAndInstallJRuby, removeObsoleteJars, vendor, generateRubySupportFiles])
    doLast {
        buildGem(projectDir, buildDir, pluginInfo.pluginFullName() + ".gemspec")
    }
}


jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }

    afterEvaluate { // (optional) : to exclude classes / packages from coverage
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(
                    dir: it,
                    exclude: ['com/ibm/guardium/spanner/CommonUtils','com/ibm/guardium/spanner/SeverityEnum'])
        })
    }
}
test.finalizedBy jacocoTestReport